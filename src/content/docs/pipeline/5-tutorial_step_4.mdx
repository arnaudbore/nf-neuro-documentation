---
title: "Part 4: Install and use a new nf-neuro module"
description: Learn how to build a simple pipeline using nf-neuro components
---

import RunIcon from '~icons/codicon/run-all';
import CommandOutputs from '../../../components/CommandOutputs.astro';
import { Steps } from '@astrojs/starlight/components';
import { Tabs, TabItem } from '@astrojs/starlight/components';
import { FileTree } from '@astrojs/starlight/components';
import { Code } from '@astrojs/starlight/components';
import { Card } from '@astrojs/starlight/components';
import Runtime from "@mdx-js/runtime";

:::tip[What to expect from this walkthrough]
In this tutorial, you'll learn how to create a basic pipeline workflow, using components from `nf-neuro`. 

By following part 4, you will learn to use some nf-core command line but you will also learn to install,
use and configure a `nf-neuro` module.
:::

### 1. List available modules

Use the nf-core modules list command to check available modules. 
It supports remote (for online repositories) and local (for installed modules).
You can filter modules using `category` name (bundle, denoising, reconst, ...).
To list all modules available on nf-neuro/modules, you can use `nf-core modules list remote`, 
which will print all available modules to the terminal.

   <CommandOutputs>
   <span slot="command">
    ```bash
       # List all modules
       nf-core modules list remote
    ```
   </span>
   <span slot="output">
    ```
    INFO     Modules available from https://github.com/scilus/nf-neuro.git (main):                                                                                                 
                                                                                                                                                                                    
    ┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓
    ┃ Module Name                      ┃
    ┡━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┩
    │ betcrop/antsbet                  │
    │ betcrop/fslbetcrop               │
    │ betcrop/synthbet                 │
    │ bundle/centroid                  │
    │ bundle/coloring                  │
    │ bundle/fixelafd                  │
    │ bundle/labelmap                  │
    │ bundle/recognize                 │
    │ bundle/stats                     │
    │ bundle/uniformize                │
    │ connectivity/afdfixel            │
    │ connectivity/decompose           │
    │ connectivity/visualize           │
    │ denoising/mppca                  │
    │ denoising/nlmeans                │
    │ image/applymask                  │
    │ image/burnvoxels                 │
    │ image/convert                    │
    │ image/cropvolume                 │
    │ image/powderaverage              │
    │ image/resample                   │
    │ io/nii2dcm                       │
    │ io/readbids                      │
    │ preproc/eddy                     │
    │ preproc/gibbs                    │
    │ preproc/n4                       │
    │ preproc/normalize                │
    │ preproc/topup                    │
    │ reconst/diffusivitypriors        │
    │ reconst/dtimetrics               │
    │ reconst/fodf                     │
    │ reconst/freewater                │
    │ reconst/frf                      │
    │ reconst/ihmt                     │
    │ reconst/meanfrf                  │
    │ reconst/noddi                    │
    │ reconst/shmetrics                │
    │ reconst/shsignal                 │
    │ registration/anattodwi           │
    │ registration/ants                │
    │ registration/antsapplytransforms │
    │ registration/convert             │
    │ registration/easyreg             │
    │ registration/synthregistration   │
    │ registration/tractogram          │
    │ segmentation/fastseg             │
    │ segmentation/fastsurfer          │
    │ segmentation/freesurferseg       │
    │ segmentation/fsreconall          │
    │ segmentation/synthseg            │
    │ stats/metricsinroi               │
    │ tracking/localtracking           │
    │ tracking/pfttracking             │
    │ tractogram/densitymap            │
    │ tractogram/removeinvalid         │
    │ tractogram/resample              │
    │ utils/extractb0                  │
    └──────────────────────────────────┘
    ```
    </span>
    </CommandOutputs>


:::note
On a first run of the commands, you may get prompted to configure some aspects of `nf-core`. You can accept every
prompt you see.
:::

### 2. Install a new module

Now, you can install the modules you want to include in your pipeline. 
Let's install the `denoising/mppca` module for DWI denoising. 
You can install the module using `nf-core modules install` command.
The new module will be installed to the `./modules/nf-neuro/modules/` directory.


   <CommandOutputs>
   <span slot="command">
    ```bash
        nf-core modules install denoising/mppca
    ```
   </span>
   <span slot="output">
   You should see this output:
    ```
                                          ,--./,-.
          ___     __   __   __   ___     /,-._.--~\
    |\ | |__  __ /  ` /  \ |__) |__         }  {
    | \| |       \__, \__/ |  \ |___     \`-._,-`-,
                                          `._,._,'

    nf-core/tools version 2.14.1 - https://nf-co.re
    There is a new version of nf-core/tools available! (3.2.0)


    INFO     Installing 'denoising/mppca'                                                                                                                                        
    INFO     Use the following statement to include this module:                                                                                                                   
                                                                                                                                                                                
    include { DENOISING_MPPCA } from '../modules/nf-neuro/denoising/mppca/main'  
    ```
    </span>
    </CommandOutputs>

:::note
By default nf-core prints the include command with a `../modules/[...]`.
In this tutorial, you need to remove a directory, like this `./modules/[...]`.
:::


### 3. Configure and use the module

The purpose of adding this module is to denoise the DWI image before computing the DTI metrics.
To do this, simply repeat the sequence of sub-steps seen in 
[**Part 3: Use and configure a nf-neuro module**](/nf-neuro/pipeline/tutorial_step_3).

<Steps>

1. Include the module in your `main.nf` : `include { DENOISING_MPPCA } from './modules/nf-neuro/denoising/mppca/main'`

2. Use the module in the workflow. The MPPCA denoising module requires only the DWI image, opposed to the DTI_METRICS
   modules which takes the dwi volume with its bval/bvec directly. Both can take a mask as input, but it is optional.

    The challenge is to leverage Nextflow tools to restructure the input data into individual channels, 
    making it easier to handle. The goal is to efficiently extract individual files, 
    such as the `dwi` image or the `bval` and `bvec` files.
    To achieve this, we'll use Nextflow's [multiMap](https://www.nextflow.io/docs/latest/reference/operator.html#multimap)
    operator to generate multiple output channels from a single input channel, as shown below.

    <Tabs>
    <TabItem label="Before">
    ```nextflow
    workflow {
        // ** Now call your input workflow to fetch your files ** //
        data = get_data()
        input_dti_metric = data.dwi.map{ it + [[]] }
        input_dti_metric.view()
        RECONST_DTIMETRICS( input_dti_metric )
    }
    ```
    </TabItem>
    <TabItem label="After">
    ```nextflow
    workflow {
        // ** Now call your input workflow to fetch your files ** //
        data = get_data()
        ch_dwi_bvalbvec = data.dwi
            .multiMap { meta, dwi, bval, bvec ->
                dwi:            [ meta, dwi ]
                bvs_files:      [ meta, bval, bvec ]
                dwi_bval_bvec:  [ meta, dwi, bval, bvec ]
        }
        ch_denoise_dwi = ch_dwi_bvalbvec.dwi
                            .map{ it + [[]] }
    }    
    ```
    </TabItem>
    </Tabs>

    :::tip[Let's break down these changes]
    <html>
    <li>`data.dwi` is the input channel from `get_data` containing: `meta`, `dwi`, `bval`, and `bvec`.</li>
    
    The [multiMap](https://www.nextflow.io/docs/latest/reference/operator.html#multimap) creates three new entries in different output channels:
    
    <dd>
        <li>`dwi`: This output contains the meta information and the DWI file.</li>
        <li>`bvs_files`: This output contains the meta information, the b-value file, and the b-vectors file.</li>
        <li>`dwi_bval_bvec`: This output contains all four elements from the input.</li>
    </dd>
    <li>`ch_dwi_bvalbvec` : Allows easy access to different file groups</li>
    
    The result of this operation is a multi-channel object, and you can access these channels as follows:
    <dd>
        <li>`ch_dwi_bvalbvec.dwi` : `[meta, dwi]`</li>
        <li>`ch_dwi_bvalbvec.bvs_files` : `[meta, bval, bvec]`</li>
        <li>`ch_dwi_bvalbvec.dwi_bval_bvec` : `[meta, dwi, bval, bvec]`</li>
    </dd>
    </html>
    :::

    :::note[Why Use `multiMap`?]

    This approach is useful in Nextflow workflows when different processes require distinct subsets or 
    different combinations of your input data.
    With `multiMap`, you can also efficiently provide the right data to each process without duplicating it in your workflow.

    As in this tutorial, you might have one process that only needs the dwi file, another that needs the bval 
    and bvec files, and a third that needs all of them. 

3. Configure the denosing module in the `nextflow.config` using the [API Documentation](/nf-neuro/api/modules/denoising/mppca) as a reference.

4. Adapt the input for `RECONST_DTIMETRICS` module to attach it after the newly included denoised DWI module, reusing its outputs as inputs.

    <Tabs>
    <TabItem label="Before">
    ```nextflow
    workflow {
        // ** Now call your input workflow to fetch your files ** //
        data = get_data()
        ch_dwi_bvalbvec = data.dwi
            .multiMap { meta, dwi, bval, bvec ->
                dwi:            [ meta, dwi ]
                bvs_files:      [ meta, bval, bvec ]
                dwi_bval_bvec:  [ meta, dwi, bval, bvec ]
        }
        ch_denoise_dwi = ch_dwi_bvalbvec.dwi
                            .map{ it + [[]] }
    } 
    ```
    </TabItem>
    <TabItem label="After">
    ```nextflow
    workflow {
        // ** Now call your input workflow to fetch your files ** //
        data = get_data()
        ch_dwi_bvalbvec = data.dwi
            .multiMap { meta, dwi, bval, bvec ->
                dwi:            [ meta, dwi ]
                bvs_files:      [ meta, bval, bvec ]
                dwi_bval_bvec:  [ meta, dwi, bval, bvec ]
        }
        ch_denoise_dwi = ch_dwi_bvalbvec.dwi
                            .map{ it + [[]] }

        DENOISING_MPPCA( ch_denoise_dwi )
        ch_dwi_denoised = DENOISING_MPPCA.out.image

        // You can now reuse the outputs and supply them to another module!
        // Update the input for RECONST_DTIMETRICS with DWI denoised output
        input_dti_denoised = ch_dwi_denoised
                .join(ch_dwi_bvalbvec.bvs_files)
                .map{ it + [[]] }

        // Update input name
        RECONST_DTIMETRICS( input_dti_denoised )
    }
    ```
    </TabItem>
    </Tabs>

    :::tip[Let's break down these changes]
    <html>
    <li>`DENOISING_MPPCA.out.image` : stores the denoised output.</li>
      
        Assign the output from `DENOISING_MPPCA` to a new channel named `ch_dwi_denoised`.
        Specifically, it's taking the `image` output.

        The **name** of a module's **output(s)** can be determined via the [API Documentation](/nf-neuro/api/categories) in the `Outputs` section.

    <li>`.join(ch_dwi_bvalbvec.bvs_files)`: merges it with `bval/bvec` data</li>
    
        The [`join`](https://www.nextflow.io/docs/latest/reference/operator.html#join)
        operator is used here to combine the items from `ch_dwi_denoised` with items from another channel `ch_dwi_bvalbvec.bvs_files`.
        The [`join`](https://www.nextflow.io/docs/latest/reference/operator.html#join) operation matches items from both channels based 
        on a common key (by default, the first element of each item).

    <li>`.map{ it + [[]] }`:  Adds an empty list `([])` to each input tuple.</li>
    
        After joining, we use `map` to transform each item in the channel, and adding an empty list `[[]]`,
        for the optional mask input. The `map` operation is similar to the one we did for the DTI module in [**Part 2**](/nf-neuro/pipeline/tutorial_step_2).
    </html>
    :::
</Steps>


### 4. Verify your files

    <Tabs>
    <TabItem label="Expected main.nf">
    ```nextflow
    #!/usr/bin/env nextflow

    include { RECONST_DTIMETRICS } from './modules/nf-neuro/reconst/dtimetrics/main'
    include { DENOISING_MPPCA } from './modules/nf-neuro/denoising/mppca/main'

    workflow get_data {
        main:
            if ( !params.input ) {
                log.info "You must provide an input directory containing all images using:"
                log.info ""
                log.info "    --input=/path/to/[input]   Input directory containing your subjects"
                log.info "                        |"
                log.info "                        ├-- S1"
                log.info "                        |    └-- ses-01"
                log.info "                        |    |    ├-- anat"
                log.info "                        |    |    |   |--*t1.nii.gz"
                log.info "                        |    |    |--dwi"
                log.info "                        |    |    |   |--*dwi.nii.gz"
                log.info "                        |    |    |   ├-- *dwi.bval"
                log.info "                        |    |    |   └-- *dwi.bvec"
                log.info "                        |    └-- ses-02"            
                log.info "                        └-- S2"
                log.info "                             └-- ses-01"
                log.info "                             |     ├-- anat"
                log.info "                             |    |   |--*t1.nii.gz"
                log.info "                             |    |--dwi"
                log.info "                             |    |   |--*dwi.nii.gz"
                log.info "                             |    |   ├-- *dwi.bval"
                log.info "                             |    |   └-- *dwi.bvec"
                log.info "                             └-- ses-02" 
                log.info ""
                error "Please resubmit your command with the previous file structure."
            }

            input = file(params.input)
            // ** Loading all files. ** //
            dwi_channel = Channel.fromFilePairs("$input/**/**/dwi/*dwi.{nii.gz,bval,bvec}", size: 3, flat: true)
                { it.parent.parent.parent.name + "_" + it.parent.parent.name}
                .map{ sid, bvals, bvecs, dwi -> [ [id: sid], dwi, bvals, bvecs ] }
        emit:
            dwi = dwi_channel
    }

    workflow {
        inputs = get_data()

        // Use Multimap to split the tuple into multi inputs structure
        ch_dwi_bvalbvec = inputs.dwi
            .multiMap { meta, dwi, bval, bvec ->
                dwi:            [ meta, dwi ]
                bvs_files:      [ meta, bval, bvec ]
                dwi_bval_bvec:  [ meta, dwi, bval, bvec ]
            }

        // Denoising DWI
        input_dwi_denoise = ch_dwi_bvalbvec.dwi
                        .map{ it + [[]] }

        DENOISING_MPPCA( input_dwi_denoise )

        // Fetch specific output
        ch_dwi_denoised = DENOISING_MPPCA.out.image

        // Input DTI update with DWI denoised output
        input_dti_denoised = ch_dwi_denoised
                .join(ch_dwi_bvalbvec.bvs_files)
                .map{ it + [[]] }

        RECONST_DTIMETRICS( input_dti_denoised )
    }
    ```
    </TabItem>
    <TabItem label="Expected nextflow.config">
    ```nextflow
    profiles {
        docker {
            docker.enabled          = true
            conda.enabled           = false
            singularity.enabled     = false
            podman.enabled          = false
            shifter.enabled         = false
            charliecloud.enabled    = false
            apptainer.enabled       = false
            docker.runOptions       = '-u $(id -u):$(id -g)'
        }
    }

    manifest {
        name            = 'scilus/nf-neuro-tutorial'
        description     = """nf-neuro-tutorial is a Nextflow pipeline for processing neuroimaging data."""
        version         = '0.1dev'
    }

    params.input      = false
    params.output     = 'result'

    process {

        publishDir = { "${params.output}/$meta.id/${task.process.replaceAll(':', '-')}" }

        withName: "DENOISING_MPPCA" {
            ext.extent = 3
        }

        withName: "RECONST_DTIMETRICS" {
            ext.ad = false
            ext.evecs = false
            ext.evals = false
            ext.fa = true
            ext.ga = false
            ext.rgb = false
            ext.md = true
            ext.mode = false
            ext.norm = false
            ext.rd = false
            ext.tensor = false
            ext.nonphysical = false
            ext.pulsation = false
            ext.residual = false
            ext.b0_thr_extract_b0 = 10
            ext.dwi_shell_tolerance = 50
            ext.max_dti_shell_value = 1200
            ext.run_qc = false
        }
    }
    ```
    </TabItem>
    </Tabs>

    ### 5. Run nextflow
    <RunIcon class="inline-icon text-blue-300" aria-hidden /> **Now, you can run nextflow..**

    <CommandOutputs>
    <span slot="command">
    ```bash
    nextflow run main.nf --input data -profile docker
    ```
    </span>
    <span slot="output">
    ```bash
    N E X T F L O W   ~  version 24.10.4

    Launching `main.nf` [astonishing_borg] DSL2 - revision: d69b63f305

    executor >  local (2)
    [ec/62dd72] process > DENOISING_MPPCA (sub-003_ses-01)    [100%] 1 of 1 ✔
    [6e/837a31] process > RECONST_DTIMETRICS (sub-003_ses-01) [100%] 1 of 1 ✔
    Completed at: Date Hour
    Duration    : 1m 19s
    CPU hours   : (a few seconds)
    Succeeded   : 2
    ```
    </span>
    </CommandOutputs>

    Check your resulting images in the results folder!
